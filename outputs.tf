output "nic_list" {
  description = "List of nic statuses"
  value       = coalescelist(nutanix_virtual_machine.vm-linux[*].nic_list_status, nutanix_virtual_machine.vm-windows[*].nic_list_status)
}

output "project_id" {
  description = "project_id generated by looking up var.project_name"
  value       = local.project_id
}

output "host_inventory" {
  description = "Map of hosts and their metadata"
  value = zipmap(
    var.vm_name,
    coalescelist(
      [for vm in nutanix_virtual_machine.vm-linux[*] :
        {
          id            = vm.metadata.uuid
          ip            = vm.nic_list_status.0["ip_endpoint_list"].0["ip"]
          creation_time = vm.metadata.creation_time
        }
      ],
      [for vm in nutanix_virtual_machine.vm-windows[*] :
        {
          id            = vm.metadata.uuid
          ip            = vm.nic_list_status.0["ip_endpoint_list"].0["ip"]
          creation_time = vm.metadata.creation_time
        }
      ]
    )
  )
}
